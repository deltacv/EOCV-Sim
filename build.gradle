import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        kotlin_version = "1.5.10"
        kotlinx_coroutines_version = "1.5.0-native-mt"
        opencv_version = "4.5.1-2"
        apriltag_plugin_version = "1.1.1"

        classgraph_version = "4.8.131"
        opencsv_version = "5.5.2"

        env = findProperty('env') == 'release' ? 'release' : 'dev'

        println("Current build is: $env")
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'com.github.deltacv'
    version '3.3.2'

    ext {
        standardVersion = version
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(Jar) {
        manifest {
            attributes['Main-Class'] = 'com.github.serivesmejia.eocvsim.Main'
        }
    }

    if(env == 'dev') {
        String date = DateTimeFormatter.ofPattern(
            "yyMMdd-HHmm"
        ).format(LocalDateTime.now())

        String hash = findProperty('hash')

        version += "-dev-${hash ?: date}"
        println("Final version of ${project} is $version")

        File libsFolder = Paths.get(
            projectDir.absolutePath, 'build', 'libs'
        ).toFile()

        for(file in libsFolder.listFiles()) {
             if(file.name.contains("dev") && file.name.endsWith(".jar"))
                file.delete()
        }
    }
}
