import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        kotlin_version = "1.5.31"
        kotlinx_coroutines_version = "1.5.0-native-mt"
        slf4j_version = "1.7.32"
        log4j_version = "2.17.1"
        opencv_version = "4.5.1-2"
        apriltag_plugin_version = "1.1.1"

        classgraph_version = "4.8.108"
        opencsv_version = "5.5.2"

        env = findProperty('env') == 'release' ? 'release' : 'dev'

        println("Current build is: $env")
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.1'
    }
}

allprojects {
    group 'com.github.deltacv'
    version '3.4.0'

    ext {
        standardVersion = version
    }

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://jitpack.io" }
        maven { url 'https://maven.openimaj.org/' }
        maven { url 'https://maven.ecs.soton.ac.uk/content/repositories/thirdparty/' }
    }

    tasks.withType(Jar) {
        manifest {
            attributes['Main-Class'] = 'com.github.serivesmejia.eocvsim.Main'
        }
    }

    if(env == 'dev') {
        String date = DateTimeFormatter.ofPattern(
            "yyMMdd-HHmm"
        ).format(LocalDateTime.now())

        String hash = findProperty('hash')

        version += "-dev-${hash ?: date}"
        println("Final version of ${project} is $version")

        File libsFolder = Paths.get(
            projectDir.absolutePath, 'build', 'libs'
        ).toFile()

        for(file in libsFolder.listFiles()) {
             if(file.name.contains("dev") && file.name.endsWith(".jar"))
                file.delete()
        }
    }
}
