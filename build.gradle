import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        kotlin_version = "2.3.0"
        kotlinx_coroutines_version = "1.10.2"
        slf4j_version = "2.0.16"
        log4j_version = "2.24.1"
        opencv_version = "4.7.0-0"
        apriltag_plugin_version = "2.1.0-D"
        papervision_version = "1.1.0"

        flatlaf_version = "3.5.1"
        miglayout_version = "11.4.2"
        skiko_version = "0.8.15"

        classgraph_version = "4.8.112"
        opencsv_version = "5.5.2"
        toml4j_version = "0.7.2"
        picocli_version = "4.6.1"

        Penv = findProperty('env')
        if(Penv != null && (Penv != 'dev' && Penv != 'release')) {
            throw new GradleException("Invalid env property, must be 'dev' or 'release'")
        }

        env = Penv == 'release' ? 'release' : 'dev'

        println("Current build is: $env")
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradleup.shadow:com.gradleup.shadow.gradle.plugin:9.3.1"
        classpath 'io.github.fvarrui:javapackager:1.7.6'
    }
}

plugins {
    id 'java'
}

allprojects {
    group 'org.deltacv.EOCV-Sim'
    version '4.1.1'

    apply plugin: 'java'

    ext {
        standardVersion = version
    }

    repositories {
        mavenCentral()
        mavenLocal()

        google()
        maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
        maven { url 'https://artifacts.openmicroscopy.org/artifactory/ome.releases/' }
    }

    tasks.withType(Jar).configureEach {
        manifest {
            attributes['Main-Class'] = 'com.github.serivesmejia.eocvsim.Main'
        }
    }

    if(env == 'dev') {
        String date = DateTimeFormatter.ofPattern(
            "yyMMdd-HHmm"
        ).format(LocalDateTime.now())

        String hash = findProperty('hash')

        version += "-dev-${hash ?: date}"
        println("Final version of ${project} is $version")

        File libsFolder = Paths.get(
            projectDir.absolutePath, 'build', 'libs'
        ).toFile()

        for(file in libsFolder.listFiles()) {
             if(file.name.contains("dev") && file.name.endsWith(".jar"))
                file.delete()
        }
    }
}