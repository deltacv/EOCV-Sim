import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
}

apply from: '../build.common.gradle'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

ext.kotest_version = '4.4.3'

test {
    useJUnitPlatform()
}

apply from: '../test-logging.gradle'

dependencies {
    implementation project(':Common')
    implementation project(':Ipc')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation "org.eclipse.jdt:ecj:3.21.0"

    implementation "org.openpnp:opencv:$opencv_version"

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-log4j12:1.7.32'
    implementation('org.openimaj:core-video-capture:1.3.10') {
        //exclude group: 'org.openimaj', module: 'core'
        exclude group: 'org.openimaj', module: 'core-audio'
        exclude group: 'org.openimaj', module: 'core-math'
        exclude group: 'org.openimaj.content', module: 'animation'
        exclude group: 'com.twelvemonkeys.common', module: 'common-lang'
        exclude group: 'com.twelvemonkeys.imageio', module: 'imageio-core'
        exclude group: 'com.twelvemonkeys.imageio', module: 'imageio-jpeg'
        exclude group: 'com.sun.media', module: 'jai-codec'
        exclude group: 'javax.media', module: 'jai-codec'
        exclude group: 'net.sourceforge.jeuclid', module: 'jeuclid'
        exclude group: 'net.sourceforge.jeuclid', module: 'jeuclid-core'
        exclude group: 'uk.ac.ed.ph.snuggletex', module: 'snuggletex-core'
        exclude group: 'com.googlecode.json-simple', module: 'json-simple'
        exclude group: 'com.flickr4java', module: 'flickr4java'
        exclude group: 'uk.ac.ed.ph.snuggletex', module: 'snuggletex-upconversion'
        // exclude group: 'org.apache.sanselan', module: 'sanselan'
        exclude group: 'com.caffeineowl', module: 'bezier-utils'
    }

    implementation "org.java-websocket:Java-WebSocket:$javawebsocket_version"

    implementation "com.github.deltacv:AprilTagDesktop:$apriltag_plugin_version"

    implementation 'info.picocli:picocli:4.6.1'
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "io.github.classgraph:classgraph:$classgraph_version"

    implementation 'com.formdev:flatlaf:1.2'
    implementation 'com.formdev:flatlaf-intellij-themes:1.2'

    implementation 'net.lingala.zip4j:zip4j:2.8.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-swing:$kotlinx_coroutines_version"

    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
}

task(writeBuildClassJava) {

    String date = DateTimeFormatter.ofPattern("yyyy-M-d hh:mm:ss").format(LocalDateTime.now())

    File versionFile = Paths.get(
        projectDir.absolutePath, 'src', 'main', 'java',
        'com', 'github', 'serivesmejia', 'eocvsim', 'Build.java'
    ).toFile()

    versionFile.delete()

    versionFile << "package com.github.serivesmejia.eocvsim;\n" +
        "\n" +
        "/*\n" +
        " * Autogenerated file! Do not manually edit this file, as\n" +
        " * it is regenerated any time the build task is run.\n" +
        " *\n" +
        " * Based from PhotonVision PhotonVersion generator task\n"+
        " */\n" +
        "@SuppressWarnings(\"ALL\")\n" +
        "public final class Build {\n" +
        "    public static final String versionString = \"$version\";\n" +
        "    public static final String standardVersionString = \"$standardVersion\";\n" +
        "    public static final String buildDate = \"$date\";\n" +
        "    public static final boolean isDev = ${version.contains("dev")};\n\n" +
        "    public static final String opencvVersion = \"$opencv_version\";\n" +
        "    public static final String apriltagPluginVersion = \"$apriltag_plugin_version\";\n" +
        "}"
}

build.dependsOn writeBuildClassJava
